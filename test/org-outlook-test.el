;;; org-outlook-test.el --- tests for org-outlook

(add-to-list 'load-path (expand-file-name "../test/support" (file-name-directory load-file-name)))
(add-to-list 'load-path (expand-file-name ".." (file-name-directory load-file-name)))

(require 'ert)
(require 'subr-x)
(require 'org)
(require 'org-outlook)

(ert-deftest org-outlook-build-auth-url-includes-scope ()
  (let ((org-outlook-client-id "client-id")
        (org-outlook-code-challenge "challenge-123")
        (org-outlook-auth-url "https://login.example.com/authorize")
        (org-outlook-resource-url "https://graph.microsoft.com/Calendars.ReadWrite"))
    (let ((url (org-outlook--build-auth-url)))
      (should (string-match-p "client_id=client-id" url))
      (should (string-match-p "code_challenge=challenge-123" url))
      (should (string-match-p "redirect_uri=http%3A%2F%2Flocalhost%3A9004" url))
      (should (string-match-p "scope=offline_access%20https%3A%2F%2Fgraph.microsoft.com%2FCalendars.ReadWrite" url)))))

(ert-deftest org-outlook-request-authorization-opens-browser ()
  (let ((org-outlook--auth-server nil)
        (org-outlook--auth-timer nil)
        (opened-url nil)
        (stopped nil)
        (cancelled nil))
    (cl-letf (((symbol-function 'start-auth-code-server) (lambda () 'server))
              ((symbol-function 'run-at-time) (lambda (&rest _args) 'timer))
              ((symbol-function 'ws-stop) (lambda (&rest _args) (setq stopped t)))
              ((symbol-function 'cancel-timer) (lambda (&rest _args) (setq cancelled t)))
              ((symbol-function 'accept-process-output) (lambda (&rest _args)
                                                          (setq org-outlook--auth-complete t)))
              ((symbol-function 'org-outlook-auth-token) (lambda () "token"))
              ((symbol-function 'org-outlook-open-link) (lambda (url) (setq opened-url url))))
      (let ((result (org-outlook-request-authorization)))
        (should (equal result "token"))
        (should opened-url)
        (should stopped)
        (should cancelled)
        (should (null org-outlook--auth-server))
        (should (null org-outlook--auth-timer))))))

(ert-deftest org-outlook-link-paragraphs-include-browser-links ()
  (let ((links (org-outlook--link-paragraphs "https://outlook" "https://teams")))
    (should (member "[[https://outlook][Open in Outlook]]" links))
    (should (member "[[https://teams][Join Teams meeting]]" links)))
  (let ((links (org-outlook--link-paragraphs nil "none")))
    (should (equal links nil))))

(ert-deftest org-outlook-open-event-link-prefers-teams ()
  (let ((opened nil))
    (cl-letf (((symbol-function 'org-outlook-get-appointment-property)
               (lambda (prop) (when (string= prop "URL") "https://outlook")))
              ((symbol-function 'org-outlook-get-prop-from-agenda)
               (lambda (_prop) nil))
              ((symbol-function 'org-outlook-open-link)
               (lambda (url) (setq opened url))))
      (org-outlook-open-event-link t)
      (should (equal opened "https://outlook"))))
  (let ((opened nil))
    (cl-letf (((symbol-function 'org-outlook-get-appointment-property)
               (lambda (_prop) nil))
              ((symbol-function 'org-outlook-get-prop-from-agenda)
               (lambda (prop) (if (string= prop "TEAMSURL") "https://teams" "https://outlook")))
              ((symbol-function 'org-outlook-open-link)
               (lambda (url) (setq opened url))))
      (org-outlook-open-event-link)
      (should (equal opened "https://teams")))))

(provide 'org-outlook-test)
;;; org-outlook-test.el ends here
